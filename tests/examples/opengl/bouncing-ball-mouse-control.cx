package main

import "gl"
import "glfw"

var width i32 = 800
var height i32 = 600

type Ball struct {
	x f32
	y f32
	vx f32
	vy f32
	gravity f32
	radius f32
}

var ball Ball

var prevTime f32
var currTime f32
var timeDelta f32
func updateTiming() () {
	// prevent 1st delta from being large, which would make moving objects teleport unpredictably 
	if prevTime == 0.0 {
		prevTime = f64.f32(glfw.GetTime())
	}

	currTime = f64.f32(glfw.GetTime())
	timeDelta = currTime - prevTime
	prevTime = currTime
}

func drawBall (ball Ball) () {
	var full_angle f32
	full_angle = f32.mul(2.0, 3.141592654)
	var x f32
	var y f32

	gl.Begin(gl.POLYGON)
	gl.Color3f(1.0, 1.0, 1.0)

	var i f32
	for i = 0.0; f32.lt(i, 20.0); i = f32.add(i, 1.0) {
		x = f32.add(ball.x, f32.mul(ball.radius, f32.cos(f32.div(f32.mul(i, full_angle), 20.0))))
		y = f32.add(ball.y, f32.mul(ball.radius, f32.sin(f32.div(f32.mul(i, full_angle), 20.0))))

		gl.Vertex2f(x, y)
	}

	gl.End()
}

func ballJumpHeight (window str, key i32, action i32, mods i32) {
	if i32.eq(key, glfw.MouseButtonLeft) {
		if action == glfw.Press {
			ball.vy = f32.add(ball.vy, -0.1)
		}
	}
	if i32.eq(key, glfw.MouseButtonRight) {
		if action == glfw.Press {
			ball.vy = f32.add(ball.vy, 0.1)
		}
	}
}

func ballDirection (window str, xpos f64, ypos f64) {
	var proportion f64
	proportion = f64.sub(f64.div(xpos, i32.f64(width)), 0.5D)

	if f64.gt(proportion, f64.add(f32.f64(ball.x), 0.01D)) {
		// go right
		ball.vx = 0.01
	} else {
		// go left
		ball.vx = -0.01
	}
}

func main () {
	glfw.Init()

	glfw.CreateWindow("window", width, height, "Bouncing Ball")
	glfw.MakeContextCurrent("window")
	
	gl.Init()
	var program i32
	program = gl.CreateProgram()
	gl.LinkProgram(program)

	// glfw.SetKeyCallback("window", "ballDirection")
	glfw.SetMouseButtonCallback("window", "ballJumpHeight")
	glfw.SetCursorPosCallback("window", "ballDirection")

	ball = Ball{
		radius: 0.05,
		x: 0.0,
		y: 0.0,
		vx: 0.01,
		vy: 0.01,
		gravity: 0.01}

	var ratio f32

	str.print("\nMove the ball to the right or the left using the arrow keys.")

	for bool.not(glfw.ShouldClose("window")) {
		updateTiming()

		var bufferWidth i32
		var bufferHeight i32
		bufferWidth, bufferHeight = glfw.GetFramebufferSize("window")
		ratio = f32.div(i32.f32(bufferWidth), i32.f32(bufferHeight))

		gl.Clear(gl.COLOR_BUFFER_BIT)

		gl.UseProgram(program)
		
		gl.MatrixMode(gl.PROJECTION)
		gl.LoadIdentity()
		gl.Ortho(f32.f64(f32.mul(ratio, -1.0)), f32.f64(ratio), -1.0D, 1.0D, 1.0D, -1.0D)
		gl.MatrixMode(gl.MODELVIEW)

		//Bounce on window bottom
		if f32.lteq(f32.add(ball.y, ball.radius), -1.0) {
			ball.vy = f32.abs(ball.vy)
		} else {
			ball.vy = f32.sub(ball.vy, ball.gravity)
		}

		ball.x = f32.add(ball.x, ball.vx)
		ball.y = f32.add(ball.y, ball.vy)
		
		drawBall(ball)


		
		glfw.PollEvents()
		glfw.SwapBuffers("window")
	}
}
